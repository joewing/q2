
; Write int in x0
; Writes 4 chars
; Uses x0, x1, x2, x3, x4, x5, x6, x7
putint_buf:
  .dw   0, 0, 0, 0, 0
putint_chars:
  .dw   4
putint:
  sta   =x7
  lea   putint_buf
  add   putint_chars
  sta   =x6   ; Pointer to end of buffer
  lea   =0
  sta   @=x6
putint_loop1:
  lda   =x6
  add   =neg1
  sta   =x6
  lea   =10
  sta   =x1
  lea   $+3
  jmp   @$+1
  .dw   div
  lda   =x2
  sta   =x0
  lea   =48
  add   =x3
  sta   @=x6
  lea   putint_buf
  nor   =zero
  add   =x6
  nor   =zero
  jfc   putint_loop1
  lea   putint_buf
  sta   =x0
  lda   =x7
  ; fall through

; Write the string at x0
; Uses x0, x1
puts:
  sta   =x1
puts_loop:
  lda   @=x0
  jfc   $+2
  jmp   @=x1
  sta   @=neg1
  lea   =1
  add   =x0
  sta   =x0
  jmp   puts_loop


; Wait for a key press and return it in x0
; Destroys x0, x1
waitkey:
  sta   =x1
waitkey_loop:
  lda   @=neg1
  nor   waitkey_mask
  sta   =x0
  jfc   $+2
  jmp   waitkey_loop

waitkey_loop2:
  lda   @=neg1
  nor   waitkey_mask
  jfc   waitkey_loop2
  jmp   @=x1
waitkey_mask:
  .dw   0x000

