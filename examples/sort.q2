
.def  x0  2
.def  x1  3
.def  x2  4
.def  x3  5
.def  x4  6
.def  x5  7
.def  x6  8
.def  x7  9

  .org  16
zero:
  .dw   0
one:
  .dw   1
neg1:
  .dw   -1

  .org  0x800

  lea   $+2
  jmp   output

  lea   =to_sort_end - to_sort
  sta   =x0
  lda   data_ptr
  sta   =x1
  lea   $+2
  jmp   @sort_ptr

  lea   =0
  sta   @=neg1

  lea   $+2
  jmp   output
  jmp   $

output:
  sta   =x7
  lea   =to_sort_end - to_sort
  sta   =x0
  lda   data_ptr
  sta   =x1
output_loop:
  lda   @=x1
  sta   @=neg1
  lda   =x1
  add   =one
  sta   =x1
  lda   =x0
  nor   =zero
  add   =one
  nor   =zero
  sta   =x0
  jfc   output_loop
  jmp   @=x7

data_ptr:
  .dw   to_sort
sort_ptr:
  .dw   sort

  .align 128

to_sort:
  .dw   5
  .dw   3
  .dw   9
  .dw   34
  .dw   8
  .dw   5
  .dw   15
  .dw   2
to_sort_end:

  .align  128

; Sort x0 words starting at x1
; Destroys x0, x1, x2, x3, x4, x5, x6, x7
sort:
  sta   =x7         ; Return address in =x7
  lea   =0          ; Outer loop: x2 = 0 .. x0
sort_outer:
  sta   =x2
  nor   =zero
  add   =x0
  jfc   @=x7        ; x2 >= x0 - return
  lea   =1
  add   =x2         ; Inner loop: x3 = x2 + 1 .. x0
sort_inner:
  sta   =x3
  nor   =zero
  add   =x0         ; x0 - x3
  jfc   sort_next   ; done with inner loop if x3 >= x0
  lda   =x1
  add   =x2
  sta   =x4         ; @=x4 = x1[x2]
  lda   =x1
  add   =x3
  sta   =x5         ; @=x5 = x1[x3]
  lda   @=x5
  nor   =zero
  add   @=x4        ; @=x4 - @=x5
  jfc   sort_noswap ; swap if @=x4 < @=x5
  lda   @=x5
  sta   =x6
  lda   @=x4
  sta   @=x5
  lda   =x6
  sta   @=x4
sort_noswap:
  lea   =1
  add   =x3
  jmp   sort_inner
sort_next:
  lea   =1
  add   =x2
  jmp   sort_outer

