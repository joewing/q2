
    lda   value         ; Load the value whose sqrt to compute.
    sta   =x0           ; Store at =x1
    lda   #$+2          ; Load return address (P+2)
    jmp   @isqrt_ptr    ; Call isqrt
    hlt   #0            ; Halt

ra:
    .dw   0
x0:
    .dw   0
x1:
    .dw   0
x2:
    .dw   0

isqrt_ptr:
    .dw   isqrt
value:
    .dw   1024

    .org  0x100

; Compute the integer square root of =x0
; Returns the result in =x0.
; Destroys =x0, =x1, =x2, =ra
isqrt:
  sta   =ra
  lda   #1
  sta   =x1       ; x1 = 1
  lda   #3
  sta   =x2       ; x2 = 3
isqrt_loop:
  lda   =x0
  nor   #0
  add   =x1
  jnf   isqrt_cont  ; if x0 >= x1 continue
  shr   =x2     ; return x0 = (x2 / 2) - 1
  nor   #0
  add   #1
  nor   #0
  sta   =x0
  jmp   @=ra
isqrt_cont:
  lda   =x1
  add   =x2
  sta   =x1   ; x1 += x2
  lda   =x2
  add   #2
  sta   =x2   ; x2 += 2
  jmp   isqrt_loop

